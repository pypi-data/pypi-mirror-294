%import common.DIGIT
%import common.INT
%import common.LETTER
%import common.NUMBER
%import common.SIGNED_INT
%import common.SIGNED_NUMBER
%import common.WS
%import .monosaccharide.MONOSACCHARIDE

// ProForma specification: https://github.com/HUPO-PSI/ProForma/
// Version: June 29, 2021
proforma: (proteoform (CROSSLINK | CHIMERIC))* proteoform
CROSSLINK: "//"
CHIMERIC: "+"

proteoform: peptide ["/" charge]

peptide: mod_global* mod_unknown_pos? mod_labile* mod_n_term? (aa | mod_range)+ mod_c_term?
// TODO: Amino acid sequence ambiguity (section 4.7).

aa.10: AA [mod+ | (_MOD_L mod_label _MOD_R)]
AA: LETTER

mod_global: _MOD_GLOBAL_L (ISOTOPE | (mod "@" (AA ",")* AA)) _MOD_GLOBAL_R
ISOTOPE: INT? LETTER+ SIGNED_INT?

mod_unknown_pos: (mod ["^" MOD_COUNT])+ "?"

mod:        _MOD_L        ((mod_name | mod_accession | mod_mass | mod_formula | mod_glycan | info) mod_label? "|")* (mod_name | mod_accession | mod_mass | mod_formula | mod_glycan | info) mod_label? _MOD_R
mod_labile: _MOD_LABILE_L ((mod_name | mod_accession | mod_mass | mod_formula | mod_glycan | info)            "|")* (mod_name | mod_accession | mod_mass | mod_formula | mod_glycan | info)            _MOD_LABILE_R
MOD_COUNT: INT

mod_n_term: (mod | (_MOD_L mod_label _MOD_R)) "-"
mod_c_term: "-" (mod | (_MOD_L mod_label _MOD_R))

mod_range: MOD_RANGE_L mod_range_pos _MOD_RANGE_R mod+
mod_range_pos: (aa | mod_range)+

mod_name.2: ((CV_ABBREV ":") | (CV_ABBREV_OPT ":")?) TEXT
CV_ABBREV_OPT: "U"i | "M"i
CV_ABBREV: "R"i | "X"i | "G"i

mod_accession.5: CV_NAME ":" TEXT
CV_NAME: "UNIMOD"i | "MOD"i | "RESID"i | "XLMOD"i | "GNO"i

mod_mass.5: [(CV_ABBREV_OPT | CV_ABBREV | MOD_MASS_OBS) ":"] MOD_MASS
MOD_MASS_OBS: "Obs"i
MOD_MASS: ("+" | "-") NUMBER

mod_formula.5: "Formula:"i (_MOD_L ISOTOPE _MOD_R)* FORMULA
FORMULA: (LETTER+ SIGNED_INT? WS?)+

mod_glycan.5: "Glycan:" (monosaccharide WS?)+
monosaccharide: MONOSACCHARIDE MONOSACCHARIDE_COUNT?
MONOSACCHARIDE_COUNT: INT

info.5: "Info:"i TEXT

mod_label.3 : "#" (MOD_LABEL_XL | MOD_LABEL_BRANCH | MOD_LABEL) ["(" MOD_SCORE ")"]
MOD_LABEL_XL: "XL" MOD_LABEL
MOD_LABEL_BRANCH: "BRANCH"
MOD_LABEL: (LETTER | DIGIT)+
MOD_SCORE: SIGNED_NUMBER

charge: CHARGE [_MOD_L ion _MOD_R]
CHARGE: SIGNED_INT
ion: [TEXT ","] TEXT

TEXT: /.+/

_MOD_L: "["
_MOD_R: "]"
_MOD_LABILE_L: "{"
_MOD_LABILE_R: "}"
_MOD_GLOBAL_L: "<"
_MOD_GLOBAL_R: ">"
MOD_RANGE_L: "("
_MOD_RANGE_R: ")"
